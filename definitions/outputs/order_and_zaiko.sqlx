config {
    type: "incremental",
    database: "doctor-ilcsi",
    schema: "dm_looker_studio",
    name: "order_and_zaiko",
    bigquery: {
        partitionBy: "partition_date",
        }
}

pre_operations {
    DELETE ${self()} where partition_date = current_date('Asia/Tokyo')
}

with order_by_each_phase as (
    select
    logi_item_code, jan_code, phase_name, sum(amount) as amount
    from ${ref('order_by_phase')}
    group by logi_item_code, jan_code, phase_name
),
order_by_phase as (
    select 
    * except(phase_name),
    case
        when phase_name in ('引当待ち', '注文確認') then '未引当て'
        when phase_name in ('発送待ち', 'RSL出荷登録待ち', 'RSL出荷依頼待ち', 'RSL出荷エラー','RSL出荷依頼済み') then '引当て済み'
        else '発送済み'
    end as phase
    from order_by_each_phase
),
hikiate_order as (
    select logi_item_code, jan_code,
    MAX(CASE WHEN phase = '未引当て' THEN amount ELSE NULL END) AS Phase1,
    MAX(CASE WHEN phase = '引当て済み' THEN amount ELSE NULL END) AS Phase2,
    MAX(CASE WHEN phase = '発送済み' THEN amount ELSE NULL END) AS Phase3,
    from order_by_phase
    group by logi_item_code, jan_code
),
-- RSL在庫数を取得。
rsl_table as (
    select
    logi_item_code, jan_code, cm_stock, rsl_stock, current_date('Asia/Tokyo') as partition_date
    from ${ref('rsl_item_master')}
    where jan_code != 'TEST'
),
order_including_rsl as (
    select
        rsl_table.*,
        hikiate_order.* except(jan_code, logi_item_code)
    from rsl_table
    left join
        hikiate_order
    on hikiate_order.jan_code = rsl_table.jan_code
)
select
    logi_item_code,
    ifnull(rsl_stock,0) as rsl_stock, -- 物流在庫
    -- ifnull(z.stock, 0) + ifnull(o.Phase1, 0) + ifnull(o.Phase2, 0) as rsl_stock, -- 物流在庫
    ifnull(Phase2, 0) as hikiate_order, -- 引き当て済み、かつ、未発送在庫
    ifnull(Phase1, 0) as notyet_hikiate_order, -- 未引当注文（複数注文時の他商品が引きあたってない注文も含む）
    cm_stock as available_zaiko, -- 有効在庫数
    partition_date
from order_including_rsl
where
partition_date = current_date('Asia/Tokyo')